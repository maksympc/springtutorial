// -----------------------------------------------------------------------------
// Configuration for the root project
// -----------------------------------------------------------------------------
description = 'spitter'

apply plugin: 'base'
apply plugin: 'idea'

allprojects {
    group 'com.springtutorial'
    version '1.0'

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        mavenCentral()
        maven { url 'http://maven.springframework.org/release/' }
        maven { url 'http://maven.springframework.org/milestone/' }
        maven { url 'http://maven.springframework.org/snapshot/' }
    }
}

def junitVersion = '4.12'
def mockitoVersion = '1.10.19'
def springVersion = '3.0.7.RELEASE'
def log4jVersion = '1.2.17'
def jodaVersion = '2.9.9'
def h2DBVersion = '1.4.196'

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    // set up dedicated directories for jars and source jars.
    // this makes it easier when putting together the distribution
//    libsBinDir = new File(libsDir, 'bin')
//    libsSrcDir = new File(libsDir, 'src')

    sourceSets {
        test {
            resources {
                srcDirs = ['src/test/resources', 'src/test/java']
            }
        }
    }

    configurations {
        provided { transitive = false }
    }

    sourceSets.main.compileClasspath += configurations.provided
    sourceSets.test.compileClasspath += configurations.provided
    sourceSets.test.runtimeClasspath += configurations.provided

    dependencies {
        compile 'javax.inject:javax.inject:1'
        compile "log4j:log4j:$log4jVersion"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

}

project('spitter-domain-plain') {
    description = 'Spitter Domain'
    dependencies {}
}

project('spitter-domain-jpa') {
    description = 'Spitter Domain JPA-Annotated'
    dependencies {
        compile "javax.persistence:persistence-api:1.0"
        compile "javax.validation:validation-api:2.0.1.Final"
        compile "commons-lang:commons-lang:2.6"
        compile "org.codehaus.jackson:jackson-core-asl:1.9.13"
        compile "org.springframework:spring-context:$springVersion"
    }
}

project('spitter-persistence-common') {
    description = 'Spitter Persistence Commons'
    dependencies {
        compile project(':spitter-domain-jpa')
    }
}

project('spitter-persistence-jdbc-conventional') {
    description = 'Spitter Persistence (Traditional)'
    dependencies {
        compile project(':spitter-domain-plain')
        compile project(':spitter-persistence-common')
    }
}

project('spitter-persistence-jdbc-template') {
    description = 'Spitter Persistence (JDBC Template)'
    dependencies {
        compile "org.springframework:spring-jdbc:$springVersion"
        compile "joda-time:joda-time:$jodaVersion"
        compile project(':spitter-domain-plain')
        compile project(':spitter-persistence-common')
        testCompile "com.h2database:h2:$h2DBVersion"
    }
}

project('spitter-persistence-hibernate-contextual-sessions') {
    description = 'Spitter Persistence (Hibernate Contextual Sessions)'
    dependencies {
        compile project(':spitter-domain-jpa')
        compile project(':spitter-persistence-common')
        compile "org.springframework:spring-orm:$springVersion"
        compile "org.hibernate:hibernate-core:3.3.2.GA"
        compile "org.hibernate:hibernate-annotations:3.4.0.GA"
        compile "javassist:javassist:3.9.0.GA"
        compile "cglib:cglib-nodep:2.2"
        testCompile "com.h2database:h2:$h2DBVersion"
        testCompile "org.slf4j:slf4j-api:1.6.1"
    }
}

project('spitter-persistence-jpa') {
    description = 'Spitter Persistence (Commons JPA)'
    dependencies {
        compile project(':spitter-domain-jpa')
        compile project(':spitter-persistence-common')
        compile "org.springframework:spring-orm:$springVersion"
        compile "org.hibernate:hibernate-core:3.3.2.GA"
        compile "org.hibernate:hibernate-entitymanager:3.4.0.GA"
        compile "com.h2database:h2:$h2DBVersion"
        compile "cglib:cglib-nodep:2.2"
        compile "org.slf4j:slf4j-api:1.6.1"
        compile "javax.persistence:persistence-api:1.0"
        compile "org.slf4j:slf4j-api:1.5.8"
        compile "org.slf4j:slf4j-nop:1.5.8"
    }
}

project('spitter-service-common') {
    description = 'Spitter Service Commons'
    dependencies {
        compile project(':spitter-domain-jpa')
    }
}

project('spitter-service-declarative-tx') {
    description = 'Spitter Service (Declarative Transactions)'
    dependencies {
        compile project(':spitter-domain-jpa')
        compile project(':spitter-service-common')
        compile project(':spitter-persistence-common')
        compile "org.springframework:spring-tx:$springVersion"
        testCompile "org.easymock:easymock:3.0"
    }
}

project('spitter-service-annotation-tx') {
    description = 'Spitter Service (Annotated Transactions)'
    dependencies {
        compile project(':spitter-domain-jpa')
        compile project(':spitter-service-common')
        compile project(':spitter-persistence-common')
        compile "org.springframework:spring-tx:$springVersion"
        testCompile "org.easymock:easymock:3.0"
    }
}